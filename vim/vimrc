let g:python_host_prog = '/usr/local/var/pyenv/versions/neovim2/bin/python'
let g:python3_host_prog = '/usr/local/var/pyenv/versions/neovim3/bin/python'

" https://github.com/rust-lang/rust.vim#formatting-with-rustfmt
let g:rustfmt_autosave = 1

" ==================================== Plug ===================================
call plug#begin()

Plug 'airblade/vim-gitgutter'
Plug 'editorconfig/editorconfig-vim'
Plug 'hashivim/vim-hashicorp-tools'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'junegunn/goyo.vim'
Plug 'lifepillar/vim-solarized8'
Plug 'mattly/vim-markdown-enhancements'
Plug 'sheerun/vim-polyglot'
Plug 'scrooloose/nerdtree'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-markdown'
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-surround'
Plug 'uarun/vim-protobuf'
Plug 'w0rp/ale'
Plug 'Xuyuanp/nerdtree-git-plugin'
Plug 'zxqfl/tabnine-vim'

call plug#end()

" =============================== Basic settings ==============================
syntax on
filetype plugin indent on

set clipboard+=unnamedplus

set nocompatible
set noswapfile

set encoding=utf-8

set expandtab
set tabstop=2
set shiftwidth=2
set smartindent
set number
set ruler

set modeline

let mapleader = ","

" searching
set ignorecase
set smartcase
set showmatch
set hlsearch
set gdefault
nnoremap <leader><space> :noh<cr>

" status lines
set statusline=%<%F%h%m%r%h%w%y\ %{fugitive#statusline()}\ %{&ff}\ lin:%l\,%L\ col:%c%V\ pos:%o\ %P
set cursorline

" crutches
" set autochdir
set mouse=a

" ================================ Color scheme ===============================

set termguicolors

" solarized
set background=light
colorscheme solarized8

" highlight trailing whitespace
highlight ExtraWhitespace ctermbg=red guibg=red
match ExtraWhitespace /\s\+\%#\@<!$/
autocmd ColorScheme * highlight ExtraWhitespace ctermbg=red guibg=red

" =============================== Auto commands ===============================

" strip trailing whitespace on save
fun! <SID>StripTrailingWhitespaces()
  let l = line(".")
  let c = col(".")
  %s/\s\+$//e
  call cursor(l, c)
endfun

augroup strip_trailing_whitespace
  autocmd!
  " but not in markdown where trailing whitespace is significant
  autocmd BufWritePre * if &ft!~?'markdown' | :call <SID>StripTrailingWhitespaces() | endif
augroup END

augroup save_on_lose_focus
  autocmd!
  " save on lose focus, but don't complain if you can't
  autocmd FocusLost * silent! wa
augroup END

augroup restore_cursor
  autocmd!
  " When editing a file, always jump to the last known cursor position.
  " Don't do it when the position is invalid or when inside an event handler
  " (happens when dropping a file on gvim).
  autocmd BufReadPost *
              \ if line("'\"") > 0 && line("'\"") <= line("$") |
              \   exe "normal g`\"" |
              \ endif
augroup END

augroup vimrcEx
  autocmd!
  " Automatically load .vimrc source when saved
  autocmd BufWritePost .vimrc,vimrc,.gvimrc,gvimrc source $MYVIMRC
augroup END

" ==================== Typo prevention and other vim remaps ===================

nnoremap ; :

" let me type :W to save, cuz that always happens
cnoreabbrev W w

augroup prevent_saving_typoed_names
  autocmd!
  " https://stackoverflow.com/a/6211489
  autocmd BufWritePre [:;]* try | echoerr 'Forbidden file name: ' . expand('<afile>') | endtry
augroup END

" =============================== Plugin stuff  ===============================

let g:EditorConfig_exclude_patterns = ['fugitive://.*']

" nerdtree
" How can I map a specific key or shortcut to open NERDTree?
map <C-o> :NERDTreeToggle<CR>
augroup nerdtree
  autocmd!
  " How can I open a NERDTree automatically when vim starts up if no files were
  " specified?
  autocmd StdinReadPre * let s:std_in=1
  autocmd VimEnter * if argc() == 0 && !exists("s:std_in") | NERDTree | endif
  " How can I close vim if the only window left open is a NERDTree?
  autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
augroup END
let NERDTreeQuitOnOpen = 1

" tabnine
" disable preview window on tab completion with tabnine
" https://github.com/Valloric/YouCompleteMe#i-get-a-weird-window-at-the-top-of-my-file-when-i-use-the-semantic-engine
let g:ycm_add_preview_to_completeopt = 0
set completeopt-=preview

" ale
function! LinterStatus() abort
    let l:counts = ale#statusline#Count(bufnr(''))

    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors

    return l:counts.total == 0 ? 'OK' : printf(
    \   '%dW %dE',
    \   all_non_errors,
    \   all_errors
    \)
endfunction

set statusline+=%=%{LinterStatus()}

" gitgutter
set updatetime=250

" fzf
map <C-p> :Files<CR>
map <C-g> :Rg<CR>
let $FZF_DEFAULT_COMMAND  = 'rg --files --hidden'
